#!/usr/bin/perl

# $OpenBSD: check-common-dirs,v 1.6 2015/06/07 12:05:53 espie Exp $
# Copyright (c) 2004, 2010 Marc Espie <espie@openbsd.org>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

# check all packages in the current directory, and report common directory
# issues

use strict;
use warnings;
my ($ports1);

use FindBin;
BEGIN {
	$ports1 = $ENV{PORTSDIR} || '/usr/ports';
}
use lib ("$ports1/infrastructure/lib", "$FindBin::Bin/../lib");

use File::Spec;
use File::Path;
use File::Basename;
use OpenBSD::Mtree;
use OpenBSD::PlistScanner;

sub register_dir
{
	my ($d, $h) = @_;
	return if defined $h->{$d};
	$h->{$d} = 1;
	register_dir(dirname($d), $h);
}

package OpenBSD::PackingElement;
sub check_common_dirs
{
}

package OpenBSD::PackingElement::FileBase;
use File::Basename;
sub check_common_dirs
{
	my ($item, $t) = @_;
	my $d = File::Spec->canonpath($item->fullname);
	main::register_dir(dirname($d), $t->{need_dirs});
}

package OpenBSD::PackingElement::DirlikeObject;
sub check_common_dirs
{
	my ($item, $t) = @_;
	my $d = File::Spec->canonpath($item->fullname);
	$t->{dirs}->{$d} = 1;
}

package OpenBSD::PackingElement::Dependency;
sub check_common_dirs
{
	my ($item, $t) = @_;
	$t->{deps}->{$item->{def}} = 1;
}

package CommonDirsScanner;
our @ISA = (qw(OpenBSD::PlistScanner));
use OpenBSD::PackageInfo;

sub register_plist
{
	my ($self, $plist) = @_;
	my $pkgname = $plist->pkgname;
	$self->{db}{$pkgname} //= {
		pkgname => $pkgname,
		missing_deps => {},
		dirs => {}, 
		need_dirs => {}, 
		deps => {},
		problems => {}
	};

	$plist->check_common_dirs($self->{db}{$pkgname});
}

sub new
{
	my ($class) = @_;
	my $o = $class->SUPER::new('check-common-dirs');
	$o->{db} = {};
	$o->{mtree} = { 
	    '/usr/local/lib/X11' => 1,
	    '/usr/local/include/X11' => 1,
	    '/usr/local/lib/X11/app-defaults' => 1
	    };
	OpenBSD::Mtree::parse($o->{mtree}, '/', '/etc/mtree/4.4BSD.dist');
	OpenBSD::Mtree::parse($o->{mtree}, '/', '/etc/mtree/BSD.x11.dist');
	return $o;
}


sub display_results
{
	my $self = shift;
	$self->build_results;
	$self->progress->next;
	$self->show_results;
}

sub parent_has_dir
{
	my ($self, $db, $t, $dir) = @_;

	for my $dep (keys %{$t->{deps}}) {
		if (!defined $db->{$dep}) {
		    if (!defined $self->{missing_deps}{$dep}) {
			    $self->ui->errsay("#1 : #2 not found", $t->{pkgname},
			    	$dep);
			    $self->{missing_deps}{$dep} = 1;
		    }
		    next;
		}
		if ($db->{$dep}->{dirs}->{$dir} || 
		    $self->parent_has_dir($db, $db->{$dep}, $dir)) {
			$t->{dirs}{$dir} = 1;
			return 1;
		}
	}
	return 0;
}

sub parent_has_problem
{
	my ($db, $t, $dir) = @_;
	for my $dep (keys %{$t->{deps}}) {
		next if !defined $db->{$dep};
		if ($db->{$dep}->{problems}->{$dir}) {
			return 1;
		}
	}
	return 0;
}

sub build_results
{
	my ($self) = @_;
	my $db = $self->{db};
	my $mtree = $self->{mtree};
	my @l = keys %$db;

	$self->progress->for_list("Building results", \@l,
	    sub {
	    	my $pkgname = shift;
		my $t = $db->{$pkgname};
		for my $dir (keys(%{$t->{need_dirs}})) {
			return if $t->{dirs}{$dir};
			return if $mtree->{$dir};
			return if $self->parent_has_dir($db, $t, $dir);
			$t->{problems}{$dir} = 1;
		}
	    });
}

sub show_results
{
	my ($self) = @_;
	my $db = $self->{db};

	for my $pkgname (sort keys %$db) {
		my @l=();
		my $t = $db->{$pkgname};
		for my $dir (keys %{$t->{problems}}) {
			next if parent_has_problem($db, $t, $dir);
			push(@l, $dir);
		}
		if (@l != 0) {
			$self->ui->say("#1: #2", $pkgname, join(', ', sort @l));
		}
	}
}

sub scan
{
	my $self = shift;
	$self->SUPER::scan;
	$self->progress->set_header("Scanning extra dependencies");
	$self->progress->message("");
	my $notfound = {};
	my $db = $self->{db};
	my $todo;
	do {
		$todo = {};
		for my $pkg (keys %$db) {
			for my $dep (keys %{$db->{$pkg}{deps}}) {
				if (!defined $db->{$dep} && 
				    !defined $notfound->{$dep}) {
					$todo->{$dep} = 1;
				}
			}
		}
		for my $pkgname (keys %$todo) {
			my $true_package = $self->ui->repo->find($pkgname);
			if ($true_package) {
				my $dir = $true_package->info;
				$true_package->close;
				my $plist = OpenBSD::PackingList->fromfile($dir.CONTENTS);
				rmtree($dir);
				$self->register_plist($plist);
			} else {
				$notfound->{$pkgname} = 1;
		    }
		}
	} while (keys %$todo > 0);
	$self->progress->next;
}

package main;

my $o = CommonDirsScanner->new;
$o->run;
